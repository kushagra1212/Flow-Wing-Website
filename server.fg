bring "Vortex-module.fg"
bring File
bring Err
bring Json
bring Map
bring Vec
bring Str
bring "SystemProcess-module.fg"
var errorObject: Vortex::HttpResponseType = {
  content_type: "text/html",
  body: `
    <html>
      <body>
        <h1>Error Loading Page</h1>
      </body>
    </html>
  `,
  status: 500
}
var errorObjectPlain: Vortex::HttpResponseType = {
  content_type: "text/plain",
  body: "Internal Server Error",
  status: 500
}

fun wrap(pageHtml: str, showOpenPlayground: bool = true) -> str {
  const headerHtml: str, headerLoadErr: Err::Result = File::read("pages/components/header.html", "r")
  const footerHtml: str, footerLoadErr: Err::Result = File::read("pages/components/footer.html", "r")
  const headHtml: str, headLoadErr: Err::Result = File::read("pages/components/head.html", "r")
  var openPlayground: str, openPlaygroundLoadErr: Err::Result = File::read("pages/components/openPlayground.html", "r")
  if !showOpenPlayground {
    openPlayground = "" 
  }
  return `
    <html>
      ` + headHtml + `
      <body>
        ` + headerHtml + `
        ` + pageHtml + `
        ` + footerHtml + `
        ` + openPlayground + `
      </body>
    </html>
  `
}


fun handleHomePage(req: Vortex::HttpRequest, res: Vortex::HttpResponse) -> nthg {
  const homePage: str, homePageLoadErr: Err::Result = File::read("pages/homePage.html", "r")
  /; Handle GET request for the home page

  const downloadSection: str, downloadSectionLoadErr: Err::Result = File::read("pages/components/downloadSection.html", "r")
  var variables: Vec::String = new Vec::String()
  variables.add("download-section" + Vortex::DELIMITER + downloadSection)
  var err: Err::Result = res.send({
    status: 200,
    content_type: "text/html",
    body: Vortex::renderTemplate(wrap(homePage), variables)
  })
  variables.free()
  if err != Nir {
    print("Error sending response: ", err.getMessage(), "\n")
  }
}


fun handleDownloadPage(req: Vortex::HttpRequest, res: Vortex::HttpResponse) -> nthg {
  const downloadSection: str, downloadSectionLoadErr: Err::Result = File::read("pages/components/downloadSection.html", "r")
  var downloadSectionSpec: str
  if Str::subStrIndex(req.endpoint, "/mac-arm64") != -1 {
    const downloadArm64: str, downloadArm64Err: Err::Result = File::read("pages/download-arm64.html", "r")
    downloadSectionSpec = downloadArm64 
  } or if Str::subStrIndex(req.endpoint, "/windows-x86_64") != -1 {
    const downloadWin86_64: str, downloadWin86_64Err: Err::Result = File::read("pages/download-windows-x86_64.html", "r")
    downloadSectionSpec = downloadWin86_64 
  } or if Str::subStrIndex(req.endpoint, "/linux-x86_64") != -1 {
    const downloadLinux86_64: str, downloadLinux86_64Err: Err::Result = File::read("pages/download-linux-x86_64.html", "r")
    downloadSectionSpec = downloadLinux86_64 
  } else {
    var err: Err::Result = res.send({
      status: 200,
      content_type: "text/html",
      body: wrap(`<center>` + downloadSection + `</center>`)
    })
    return :
  }
  var err: Err::Result = res.send({
    status: 200,
    content_type: "text/html",
    body: wrap(downloadSectionSpec)
  })
}


fun handleFlowWingCodeEditor(req: Vortex::HttpRequest, res: Vortex::HttpResponse) -> nthg {
  const flowWingCodeEditor: str, flowWingCodeEditorLoadErr: Err::Result = File::read("pages/flow-wing-code-editor.html", "r")
  var err: Err::Result = res.send({
    status: 200,
    content_type: "text/html",
    body: wrap(flowWingCodeEditor, false)
  })
}


fun codeSubmit(req: Vortex::HttpRequest, res: Vortex::HttpResponse) -> nthg {
  var body: str = String(req.body)
  var len: int = strLength(body)
  var codeLength: int = Str::subStrIndex(body, "\",\"input\"")
  var code: str = ""
  var input: str = ""
  var i: int = 9
  while (i < codeLength) {
    code = code + Str::int8ToString(body[i]) 
    i = i + 1 
  }
  Str::replaceInPlace(code, "\\n", "\n")
  Str::replaceInPlace(code, "\\\"", "\"")
  Str::replaceInPlace(code, "\\\n", "\\n")
  Str::replaceInPlace(code, "\\\t", "\\t")
  Str::replaceInPlace(code, "\\\r", "\\r")
  i = i + 11 
  while (i < len - 2) {
    if (Str::compare(Str::int8ToString(body[i]), "\"") == 0 && Str::compare(Str::int8ToString(body[i + 1]), "}") == 0) {
      break
    }
    input = input + Str::int8ToString(body[i]) 
    i = i + 1 
  }

  {
    var result: str, errorExecution: Err::Result = SystemProcess::runCMD("rm -rf build/bin/code")
    var result2: str, errorExecution2: Err::Result = SystemProcess::runCMD("rm -rf code.fg")
  }
  const fileName: str = "4a7d1ed414474e4033ac29ccb8653"
  var errorWritingToFile: Err::Result = File::write(fileName + ".fg", code, "wb")
  if (errorWritingToFile != Nir) {
  var err: Err::Result = res.send({
    status: 200,
    content_type: "text/plain",
    body: "No Output"
  })
    return :
  }
  var result: str, errorExecution: Err::Result = Nir,Nir
  result, errorExecution = SystemProcess::runCMD("FlowWing " + fileName + ".fg") 
  if (errorExecution != Nir) {
    var err: Err::Result = res.send({
    status: 200,
    content_type: "text/plain",
    body: "No Output"
  })
    return :
  }
  var errorText: str = ""
  if (Str::subStrIndex(result, "File: " + fileName + ".fg [Error]") != -1) {
    var err: Err::Result = res.send({
      status: 203,
      content_type: "text/plain",
      body: ((result != Nir) ? result : "")
    })
    return :
  }
  result, errorExecution = SystemProcess::runCMD("build/bin/" + fileName, input) 
  if (errorExecution) {
    if (errorExecution.message == "Command execution timed out") {
      result = "Command execution timed out: TLE" 
    }
  }
  var err: Err::Result = res.send({
    status: 200,
    content_type: "text/plain",
    body: ((result != Nir) ? result : "")
  })
}

const port: int = 8080
print("Flow-Wing server starting on port: ", port, "\n")
var server: Vortex::Server = new Vortex::Server(port)
server.setRoute("GET", "/", handleHomePage)
server.setRoute("GET", "/downloads/*", handleDownloadPage)
server.setRoute("GET", "/flow-wing-code-editor", handleFlowWingCodeEditor)
server.setRoute("POST", "/code/submit", codeSubmit)
server.start()
